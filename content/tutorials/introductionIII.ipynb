
        {
            "cells": [
          {
           "cell_type": "markdown",
           "metadata": {},
           
           "source": ["# F# Introduction III: Library Setup\n",
"\n",
"This guide shows an example setup for a library. This is not the only way on how to do this, but merely a possibility. As always, this guide is meant as a starting point to be expanded upon. \n",
"For example, unit tests and full buildchains with automatic releases can be added to this template. \n",
"The installation of .NET 5.0 or dotnet SDK 3.1 LTS is required. It is also recommended to use [GitHub](https://github.com/) when following this example.\n",
"\n",
"## Initializing the repository\n",
"\n",
"* An easy way to initialize a repository is by creating a new one using GitHub and cloning it.\n",
"    * You can automatically add a readme, a .gitignore with many entries for Visual Studio already added and a license of choice.\n",
"\n",
"    ![](https://fslab.org/images/InitRepo.png)\n",
"\n",
"* After you cloned the initialized repository, it should look like this:  \n",
"\n",
"    ![](https://fslab.org/images/Lib1.png)\n",
"\n",
"## Initializing the library\n",
"\n",
"* The stock library template is just fine (change framework if you know what you are doing):\n",
"    `dotnet new classlib -lang F# -n \"YourNameHere\" --framework net5.0 -o src/YourNameHere`\n",
"* Add an entry for the \u0027pkg\u0027 folder to your `.gitignore`\n",
"* Create a `RELEASE_NOTES.md` file in the project root, make sure to add at least one version header like this:\n",
"\n",
"```\n",
"### 0.0.1 - 28/7/2021\n",
"```\n",
"* Add a solution to your projekt with `dotnet new sln --name YourNameHere`\n",
"* After you completed the previous steps your folder should look like this:  \n",
"\n",
"    ![](https://fslab.org/images/Lib2.png)\n",
"\n",
"## Initializing the buildchain with FAKE\n",
"\n",
"* Initialize a local tool manifest that will keep track of the usable local dotnet tools in this project.\n",
"    * In the project root: `dotnet new tool-manifest`\n",
"* In the project root: Install the fake cli as local tool: `dotnet tool install fake-cli`\n",
"* In the project root: Install paket as local tool: `dotnet tool install paket`\n",
"* In the project root: Create a new empty `build.fsx` file\n",
"* Your folder should now look like this:  \n",
"\n",
"    ![](https://fslab.org/images/Lib3.png)\n",
"\n",
"* Open the `build.fsx` file (intellisense will not work right after creating it) and add the following content.\n",
"\n",
"First, lets reference the dependencies of the build script. In fake they are loaded via the `paket` manager:\n",
"\n",
"```fsharp\n",
"#r \"paket:\n",
"nuget BlackFox.Fake.BuildTask\n",
"nuget Fake.Core.Target\n",
"nuget Fake.Core.Process\n",
"nuget Fake.Core.ReleaseNotes\n",
"nuget Fake.IO.FileSystem\n",
"nuget Fake.DotNet.Cli\n",
"nuget Fake.DotNet.MSBuild\n",
"nuget Fake.DotNet.AssemblyInfoFile\n",
"nuget Fake.DotNet.Paket\n",
"nuget Fake.DotNet.FSFormatting\n",
"nuget Fake.DotNet.Fsi\n",
"nuget Fake.DotNet.NuGet\n",
"nuget Fake.Api.Github\n",
"nuget Fake.DotNet.Testing.Expecto \n",
"nuget Fake.Tools.Git //\"\n",
"```\n",
"\n",
"Then, we open the dependencies. Note that for getting intellisense, you will have to run the script once with the fake runner (see [here](#Running-the-build-script)).\n",
"\n"]
          }
,
          {
           "cell_type": "code",
           "metadata": {},
            "execution_count": 3, "outputs": [], 
           "source": ["#if !FAKE\n",
"#load \"./.fake/build.fsx/intellisense.fsx\"\n",
"#r \"netstandard\" // Temp fix for https://github.com/dotnet/fsharp/issues/5216\n",
"#endif\n",
"\n",
"open BlackFox.Fake\n",
"open System.IO\n",
"open Fake.Core\n",
"open Fake.DotNet\n",
"open Fake.IO\n",
"open Fake.IO.FileSystemOperators\n",
"open Fake.IO.Globbing.Operators\n",
"open Fake.Tools\n",
"\n",
"[\u003cAutoOpen\u003e]\n",
"/// user interaction prompts for critical build tasks where you may want to interrupt when you see wrong inputs.\n",
"module MessagePrompts =\n",
"\n",
"    let prompt (msg:string) =\n",
"        System.Console.Write(msg)\n",
"        System.Console.ReadLine().Trim()\n",
"        |\u003e function | \"\" -\u003e None | s -\u003e Some s\n",
"        |\u003e Option.map (fun s -\u003e s.Replace (\"\\\"\",\"\\\\\\\"\"))\n",
"\n",
"    let rec promptYesNo msg =\n",
"        match prompt (sprintf \"%s [Yn]: \" msg) with\n",
"        | Some \"Y\" | Some \"y\" -\u003e true\n",
"        | Some \"N\" | Some \"n\" -\u003e false\n",
"        | _ -\u003e System.Console.WriteLine(\"Sorry, invalid answer\"); promptYesNo msg\n",
"\n",
"    let releaseMsg = \"\"\"This will stage all uncommitted changes, push them to the origin and bump the release version to the latest number in the RELEASE_NOTES.md file. \n",
"        Do you want to continue?\"\"\"\n",
"\n",
"    let releaseDocsMsg = \"\"\"This will push the docs to gh-pages. Remember building the docs prior to this. Do you want to continue?\"\"\"\n",
"\n",
"/// Executes a dotnet command in the given working directory\n",
"let runDotNet cmd workingDir =\n",
"    let result =\n",
"        DotNet.exec (DotNet.Options.withWorkingDirectory workingDir) cmd \"\"\n",
"    if result.ExitCode \u003c\u003e 0 then failwithf \"\u0027dotnet %s\u0027 failed in %s\" cmd workingDir\n"]
          }
,
          {
           "cell_type": "markdown",
           "metadata": {},
           
           "source": ["Note: This `build.fsx` will be gradually epxanded\n",
"\n",
"* Add the `ProjectInfo` module to the `build.fsx` file, which will contain all relevant metadata for the buildchain except nuget package metadata (more on that later).\n",
"* Replace all strings with the correct ones for your project.\n",
"\n"]
          }
,
          {
           "cell_type": "code",
           "metadata": {},
            "execution_count": 4, "outputs": [], 
           "source": ["/// Metadata about the project\n",
"module ProjectInfo = \n",
"\n",
"    let project = \"LibraryExample\"\n",
"\n",
"    let summary = \"An example Library\"\n",
"\n",
"    let configuration = \"Release\"\n",
"\n",
"    // Git configuration (used for publishing documentation in gh-pages branch)\n",
"    // The profile where the project is posted\n",
"    let gitOwner = \"YourGitProfile\"\n",
"    let gitName = \"YourNameHere\"\n",
"\n",
"    let gitHome = sprintf \"%s/%s\" \"https://github.com\" gitOwner\n",
"\n",
"    let projectRepo = sprintf \"%s/%s/%s\" \"https://github.com\" gitOwner gitName\n",
"\n",
"    let website = \"/YourNameHere\"\n",
"\n",
"    let pkgDir = \"pkg\"\n",
"\n",
"    let release = ReleaseNotes.load \"RELEASE_NOTES.md\"\n",
"\n",
"    let stableVersion = SemVer.parse release.NugetVersion\n",
"\n",
"    let stableVersionTag = (sprintf \"%i.%i.%i\" stableVersion.Major stableVersion.Minor stableVersion.Patch )\n",
"\n",
"    let mutable prereleaseSuffix = \"\"\n",
"\n",
"    let mutable prereleaseTag = \"\"\n",
"\n",
"    let mutable isPrerelease = false\n"]
          }
,
          {
           "cell_type": "markdown",
           "metadata": {},
           
           "source": ["* Add the `BasicTasks` module to the `build.fsx` file, which will contain the minimal build chain.\n",
"\n"]
          }
,
          {
           "cell_type": "code",
           "metadata": {},
            "execution_count": 5, "outputs": [], 
           "source": ["/// Barebones, minimal build tasks\n",
"module BasicTasks = \n",
"\n",
"    open ProjectInfo\n",
"\n",
"    let setPrereleaseTag = BuildTask.create \"SetPrereleaseTag\" [] {\n",
"        printfn \"Please enter pre-release package suffix\"\n",
"        let suffix = System.Console.ReadLine()\n",
"        prereleaseSuffix \u003c- suffix\n",
"        prereleaseTag \u003c- (sprintf \"%s-%s\" release.NugetVersion suffix)\n",
"        isPrerelease \u003c- true\n",
"    }\n",
"\n",
"    let clean = BuildTask.create \"Clean\" [] {\n",
"        !! \"src/**/bin\"\n",
"        ++ \"src/**/obj\"\n",
"        ++ \"pkg\"\n",
"        ++ \"bin\"\n",
"        |\u003e Shell.cleanDirs \n",
"    }\n",
"\n",
"    let build = BuildTask.create \"Build\" [clean] {\n",
"        !! \"src/**/*.*proj\"\n",
"        |\u003e Seq.iter (DotNet.build id)\n",
"    }\n",
"\n",
"    let copyBinaries = BuildTask.create \"CopyBinaries\" [clean; build] {\n",
"        let targets = \n",
"            !! \"src/**/*.??proj\"\n",
"            -- \"src/**/*.shproj\"\n",
"            |\u003e  Seq.map (fun f -\u003e ((Path.getDirectory f) \u003c/\u003e \"bin\" \u003c/\u003e configuration, \"bin\" \u003c/\u003e (Path.GetFileNameWithoutExtension f)))\n",
"        for i in targets do printfn \"%A\" i\n",
"        targets\n",
"        |\u003e  Seq.iter (fun (fromDir, toDir) -\u003e Shell.copyDir toDir fromDir (fun _ -\u003e true))\n",
"    }\n"]
          }
,
          {
           "cell_type": "markdown",
           "metadata": {},
           
           "source": ["* At the bottom of the `build.fsx` file, add the following lines:\n",
"\n"]
          }
,
          {
           "cell_type": "code",
           "metadata": {},
            "execution_count": 6, "outputs": [], 
           "source": ["open BasicTasks\n",
"BuildTask.runOrDefault copyBinaries\n"]
          }
,
          {
           "cell_type": "markdown",
           "metadata": {},
           
           "source": ["* Create a `build.cmd` or `build.sh` file (or both) with the following lines:\n",
"\n",
"### build.cmd\n",
"\n",
"```shell\n",
"dotnet tool restore\n",
"dotnet fake build %*\n",
"```\n",
"\n",
"### build.sh\n",
"\n",
"```shell\n",
"#!/usr/bin/env bash\n",
"\n",
"set -eu\n",
"set -o pipefail\n",
"\n",
"dotnet tool restore\n",
"dotnet fake build \"$@\"\n",
"```\n",
"\n",
"## Running the build script\n",
"\n",
"* You can now run your build via calling either `build.cmd` or `build.sh`.\n",
"    * Optionally, you can pass the `-t` argument with it to execute a specific build task, e.g `./build.cmd -t clean` to execute the clean target.\n",
"    * The first time you run the build.cmd will also enable intellisense for the fake build script\n",
"* After building for the first time your folder will look like this:  \n",
"\n",
"    ![](https://fslab.org/images/Lib4.png)\n",
"\n",
"## Packing a nuget package\n",
"\n",
"* Add nuget package metadata to the project file (src/LibraryExample/LibraryExample.fsproj) and adapt accordingly:\n",
"\n",
"```\n",
"\u003cPropertyGroup\u003e\n",
"    \u003cAuthors\u003eYourName\u003c/Authors\u003e\n",
"    \u003cDescription\u003eYour description here\u003c/Description\u003e\n",
"    \u003cSummary\u003eYour summary here\u003c/Summary\u003e\n",
"    \u003cPackageLicenseExpression\u003eMIT\u003c/PackageLicenseExpression\u003e\n",
"    \u003cPackageProjectUrl\u003ehttps://fslab.org/projectName/\u003c/PackageProjectUrl\u003e\n",
"    \u003cPackageIconUrl\u003ehttps://fslab.org/projectName/img/logo.png\u003c/PackageIconUrl\u003e\n",
"    \u003cPackageTags\u003edocumentation fsharp csharp dotnet\u003c/PackageTags\u003e\n",
"    \u003cRepositoryUrl\u003ehttps://github.com/fslaborg/projectName\u003c/RepositoryUrl\u003e\n",
"    \u003cRepositoryType\u003egit\u003c/RepositoryType\u003e\n",
"    \u003cFsDocsLicenseLink\u003ehttps://github.com/fslaborg/projectName/blob/master/LICENSE\u003c/FsDocsLicenseLink\u003e\n",
"    \u003cFsDocsReleaseNotesLink\u003ehttps://github.com/fslaborg/projectName/blob/master/RELEASE_NOTES.md\u003c/FsDocsReleaseNotesLink\u003e\n",
"\u003c/PropertyGroup\u003e\n",
"```\n",
"\n",
"* Add the `PackageTasks` module to the `build.fsx` file, which will take care of building nuget packages for both stable and prerelease packages:\n",
"\n"]
          }
,
          {
           "cell_type": "code",
           "metadata": {},
            "execution_count": 7, "outputs": [], 
           "source": ["/// Package creation\n",
"module PackageTasks = \n",
"\n",
"    open ProjectInfo\n",
"\n",
"    open BasicTasks\n",
"\n",
"    let pack = BuildTask.create \"Pack\" [clean; build; copyBinaries] {\n",
"        if promptYesNo (sprintf \"creating stable package with version %s OK?\" stableVersionTag ) \n",
"            then\n",
"                !! \"src/**/*.*proj\"\n",
"                |\u003e Seq.iter (Fake.DotNet.DotNet.pack (fun p -\u003e\n",
"                    let msBuildParams =\n",
"                        {p.MSBuildParams with \n",
"                            Properties = ([\n",
"                                \"Version\",stableVersionTag\n",
"                                \"PackageReleaseNotes\",  (release.Notes |\u003e String.concat \"\\r\\n\")\n",
"                            ] @ p.MSBuildParams.Properties)\n",
"                        }\n",
"                    {\n",
"                        p with \n",
"                            MSBuildParams = msBuildParams\n",
"                            OutputPath = Some pkgDir\n",
"                    }\n",
"                ))\n",
"        else failwith \"aborted\"\n",
"    }\n",
"\n",
"    let packPrerelease = BuildTask.create \"PackPrerelease\" [setPrereleaseTag; clean; build; copyBinaries] {\n",
"        if promptYesNo (sprintf \"package tag will be %s OK?\" prereleaseTag )\n",
"            then \n",
"                !! \"src/**/*.*proj\"\n",
"                //-- \"src/**/Plotly.NET.Interactive.fsproj\"\n",
"                |\u003e Seq.iter (Fake.DotNet.DotNet.pack (fun p -\u003e\n",
"                            let msBuildParams =\n",
"                                {p.MSBuildParams with \n",
"                                    Properties = ([\n",
"                                        \"Version\", prereleaseTag\n",
"                                        \"PackageReleaseNotes\",  (release.Notes |\u003e String.toLines )\n",
"                                    ] @ p.MSBuildParams.Properties)\n",
"                                }\n",
"                            {\n",
"                                p with \n",
"                                    VersionSuffix = Some prereleaseSuffix\n",
"                                    OutputPath = Some pkgDir\n",
"                                    MSBuildParams = msBuildParams\n",
"                            }\n",
"                ))\n",
"        else\n",
"            failwith \"aborted\"\n",
"    }\n"]
          }
,
          {
           "cell_type": "markdown",
           "metadata": {},
           
           "source": ["* You can test both targets with `./build.cmd -t Pack` or `./build.cmd -t PackPrerelease` respectively.\n",
"* The packages can be found in the `pkg` folder in the project root. Since you do not want to host your nuget packages on github, do also remove this folder from source control by adding /pkg to your .gitignore file.\n",
"* If you want users of your nuget package to have a pleasant debugging experience you can make use of [sourcelink](https://github.com/dotnet/sourcelink).\n",
"    * To install this package, navigate to the folder of your project, e.g. src/LibraryExample and call: `dotnet add package Microsoft.SourceLink.GitHub --version 1.0.0`\n",
"\n",
"## Documentation\n",
"\n",
"* In the project root: Install fsdocs as local tool: `dotnet tool install FSharp.Formatting.CommandTool`\n",
"* In the project root: Install the fslab documentation template: `dotnet new -i FsLab.DocumentationTemplate::*`\n",
"* Initialize the fslab documentation template: `dotnet new fslab-docs`\n",
"* Add the `DocumentationTasks` module to the `build.fsx` file, which will take care initializing documentation files and developing them:\n",
"\n"]
          }
,
          {
           "cell_type": "code",
           "metadata": {},
            "execution_count": 8, "outputs": [], 
           "source": ["/// Build tasks for documentation setup and development\n",
"module DocumentationTasks =\n",
"\n",
"    open ProjectInfo\n",
"\n",
"    open BasicTasks\n",
"\n",
"    let buildDocs = BuildTask.create \"BuildDocs\" [build; copyBinaries] {\n",
"        printfn \"building docs with stable version %s\" stableVersionTag\n",
"        runDotNet \n",
"            (sprintf \"fsdocs build --eval --clean --property Configuration=Release --parameters fsdocs-package-version %s\" stableVersionTag)\n",
"            \"./\"\n",
"    }\n",
"\n",
"    let buildDocsPrerelease = BuildTask.create \"BuildDocsPrerelease\" [setPrereleaseTag; build; copyBinaries] {\n",
"        printfn \"building docs with prerelease version %s\" prereleaseTag\n",
"        runDotNet \n",
"            (sprintf \"fsdocs build --eval --clean --property Configuration=Release --parameters fsdocs-package-version %s\" prereleaseTag)\n",
"            \"./\"\n",
"    }\n",
"\n",
"    let watchDocs = BuildTask.create \"WatchDocs\" [build; copyBinaries] {\n",
"        printfn \"watching docs with stable version %s\" stableVersionTag\n",
"        runDotNet \n",
"            (sprintf \"fsdocs watch --eval --clean --property Configuration=Release --parameters fsdocs-package-version %s\" stableVersionTag)\n",
"            \"./\"\n",
"    }\n",
"\n",
"    let watchDocsPrerelease = BuildTask.create \"WatchDocsPrerelease\" [setPrereleaseTag; build; copyBinaries] {\n",
"        printfn \"watching docs with prerelease version %s\" prereleaseTag\n",
"        runDotNet \n",
"            (sprintf \"fsdocs watch --eval --clean --property Configuration=Release --parameters fsdocs-package-version %s\" prereleaseTag)\n",
"            \"./\"\n",
"    }\n"]
          }
,
          {
           "cell_type": "markdown",
           "metadata": {},
           
           "source": ["* To create a new documentation file, run `./build.cmd -t InitDocsPage`\n",
"* Add `tmp/` to `.gitignore`\n",
"* To run fsdocs in watchmode (hot reaload local hosting of your docs for live development), run `dotnet fsdocs watch`\n",
"* Your repository should now look like this:  \n",
"\n",
"    ![](https://fslab.org/images/Lib5.png)\n",
"\n",
"## Adding nuget packages\n",
"\n",
"* Navigate to your project folder (i. e. `src/LibraryExample`)\n",
"* If you want to specify a package source other than nuget.com (e.g. a local package) you can specify other sources after adding a nuget.config file to your project root:\n",
"\n",
"```\n",
"\u003c?xml version=\"1.0\" encoding=\"utf-8\"?\u003e\n",
"\u003cconfiguration\u003e\n",
"\u003c/configuration\u003e\n",
"```\n",
"\n",
"* The following example would add the local lib folder as a new nuget source to your local nuget.config file: `dotnet nuget add source ./lib --configfile nuget.config`\n",
"\n",
"```\n",
"\u003c?xml version=\"1.0\" encoding=\"utf-8\"?\u003e\n",
"\u003cconfiguration\u003e\n",
"  \u003cpackageSources\u003e\n",
"    \u003cadd key=\"Package source 1\" value=\"./lib\" /\u003e\n",
"  \u003c/packageSources\u003e\n",
"\u003c/configuration\u003e\n",
"```\n",
"\n",
"* Calling `dotnet add package PackageName --version PackageVersion` will still start to search for the package on nuget.com, but if this call is unsuccesful, Package source 1 will be used as a fallback. For a more complete view on how to use nuget.config files please visit the [offical documentation](https://docs.microsoft.com/en-us/nuget/consume-packages/configuring-nuget-behavior) or have a look at [this](https://blogs.naxam.net/configure-nuget-package-sources-for-your-project-cd8b96397360) blog post.\n",
"\n"]
          }],
            "metadata": {
            "kernelspec": {"display_name": ".NET (F#)", "language": "F#", "name": ".net-fsharp"},
            "langauge_info": {
        "file_extension": ".fs",
        "mimetype": "text/x-fsharp",
        "name": "C#",
        "pygments_lexer": "fsharp",
        "version": "4.5"
        }
        },
            "nbformat": 4,
            "nbformat_minor": 1
        }
        

